// ON CHANGING THESE, ALSO CHANGE Jenkinsfile.downstream
void buildProject(String target, String cmakeOpts) {
    timeout(time: 60, activity: true, unit: 'MINUTES') {
        cmakeBuild generator: 'Ninja',\
            buildDir: 'build',\
            buildType: 'RelWithDebInfo',\
            installation: 'InSearchPath',\
            steps: [[args: target]],\
            cmakeArgs: "-DMLIR_ROCK_DRIVER_ENABLED=1 $cmakeOpts"
    }
}

void buildRock(String cmakeOpts) {
    sh '[ ! -d build ] || rm -rf build'
    cmakeBuild generator: 'Unix Makefiles',\
        buildDir: 'build',\
        buildType: 'Release',\
        installation: 'InSearchPath',\
        cmakeArgs: """-DCMAKE_CXX_COMPILER=/opt/rocm/llvm/bin/clang++
                     -DCMAKE_C_COMPILER=/opt/rocm/llvm/bin/clang
                     ${cmakeOpts}
                     """
    sh 'cd build; make -j $(nproc) RockDriver'
}

void getAndBuildRock(String prefixOpt, String cmakeOpts) {
    git branch: 'rename-MLIR', poll: false,\
        url: 'https://github.com/ROCmSoftwarePlatform/Rock.git'
    buildRock(cmakeOpts)
}

void buildRockWithMLIR() {
    buildProject('librockCompiler',
                    '-DBUILD_FAT_LIBROCKCOMPILER=ON')
    cmake arguments: "--install . --component librockCompiler --prefix ${WORKSPACE}/RockDeps",\
        installation: 'InSearchPath', workingDir: 'build'

    dir('Rock') {
        git branch: 'rename-MLIR', poll: false,\
            url: 'https://github.com/ROCmSoftwarePlatform/Rock.git'
        // Note: setting cxxflags here works around https://github.com/ROCmSoftwarePlatform/Rock/issues/1604
        buildRock("""-DROCK_USE_MLIR=On
                        -DROCK_BACKEND=HIP
                        -DCMAKE_PREFIX_PATH=${WORKSPACE}/RockDeps
                        -DCMAKE_CXX_FLAGS="-isystem ${WORKSPACE}/RockDeps/include"
                        -DROCK_USER_DB_PATH=${WORKSPACE}/Rock/build/RockUserDB
                        -DROCK_TEST_FLAGS="--verbose --disable-verification-cache"
                        """)
    }
}

void showEnv() {
    echo "$env.NODE_NAME"
    sh 'cat /etc/os-release'
    sh 'ulimit -a'
    // Ignore rocm-smi failures in ixt-sjc2-05
    sh '/opt/rocm/bin/rocm-smi || true'
    sh '/opt/rocm/bin/rocm_agent_enumerator'
    sh 'id'
}

String dockerArgs() {
    return "--device=/dev/kfd --device=/dev/dri --group-add video --group-add render -v /etc/passwd:/etc/passwd:ro -v /etc/group:/etc/group:ro"
}

String dockerImage() {
    return 'rocm/mlir:rocm5.2-latest'
}

void preMergeCheck() {
    sh '''
    if [ ! -f ./build/compile_commands.json ];  then
      echo "No compile commands, bailing."
      exit 1
    fi
    if [ ! -f ./compile_commands.json ]; then
      ln -s build/compile_commands.json compile_commands.json
    fi
    '''
    sh 'python3 ./mlir/utils/jenkins/static-checks/premerge-checks.py'
}

void testRockDriver(String dtype, String layout, String direction, String tuning) {
    echo "Config is (${dtype}, ${layout}, dir=${direction}, tuning=${tuning})"
    timeout(time: 60, activity: true, unit: 'MINUTES') {
        dir('Rock/build/') {
            sh """
            bash ${WORKSPACE}/mlir/utils/jenkins/rock-tests/rock_validate.sh \
            --layout ${layout} \
            --direction ${direction} \
            --dtype ${dtype} \
            ${tuning == '1' ? '--tuning' : '--no-tuning'} \
            < ${WORKSPACE}/mlir/utils/jenkins/rock-tests/resnet50-rock-configs"""
        }
    }
}

void postProcessPerfRes() {
    publishHTML (target: [
        allowMissing: false,
        alwaysLinkToLastBuild: false,
        keepAll: true,
        reportDir: 'build',
        reportFiles: 'MLIR_Performance_Changes.html,MLIR_vs_Rock.html',
        reportName: 'Performance report'
    ])

    plot csvFileName: 'plot-nightly-perf-results-000001.csv',\
        csvSeries: [[file: 'build/mlir_vs_rock_perf_for_plot.csv', displayTableFlag: false]],\
        title: 'Test performance summary',\
        yaxis: 'TFlops',\
        style: 'line',\
        group: 'Performance plots'

    // Save results for future comparison
    archiveArtifacts artifacts: 'build/mlir_*.csv,build/perf-run-date', onlyIfSuccessful: true
}

//makes sure multiple builds are not triggered for branch indexing
def resetBuild() {
    if (currentBuild.getPreviousBuild() == null || currentBuild.getPreviousBuild().getBuildCauses().toString().contains('BranchIndexingCause')) {
        def buildNumber = BUILD_NUMBER as int; if (buildNumber > 1) milestone(buildNumber - 1); milestone(buildNumber)
    }
}

void setHeartbeat() {
    script {
        System.setProperty("org.jenkinsci.plugins.durabletask.BourneShellScript.HEARTBEAT_CHECK_INTERVAL", "86400");
    }
}

def rebootNode() {
    build job: 'maintenance/reboot-slaves', propagate: false , parameters: [string(name: 'server', value: "${env.NODE_NAME}"),]
}

pipeline {
    agent none
    parameters {
        // Below should be set statically by Jenkins job
        booleanParam(name: 'nightly', defaultValue: params.nightly ? true : false,
                     description: 'Run extra nightly-only tests')
        booleanParam(name: 'canXdlops', defaultValue: params.canXdlops == false ? false : true,
                     description: 'Can this CI instance use xdlops (no for public server)')
        booleanParam(name: 'weekly', defaultValue: params.weekly ? true : false,
                     description: 'Run weekly-only jobs')

        // Each below control whether to run a individual stage from parallel run
        // They default to true or empty but deverloper can toggle them for debugging purpose
        booleanParam(name: 'sharedLib', defaultValue: true,
                     description: 'Run the shared library stage')
        booleanParam(name: 'staticLib', defaultValue: true,
                     description: 'Run the static library stage')
        booleanParam(name: 'perfTest', defaultValue: true,
                     description: 'Run the performance testing stage')
    }
    stages {
        stage("Set System Property") {
            steps {
                setHeartbeat()
            }
        }
        stage("Kill old PR builds") {
            when {
                equals expected: false, actual: params.weekly;
                equals expected: false, actual: params.nightly;
            }
            steps {
                resetBuild()
            }
        }
        stage("Build and Test") {
            parallel {
                stage("Shared Library: fixed E2E tests") {
                    when {
                        beforeAgent true;
                        equals expected: true, actual: params.sharedLib;
                        equals expected: false, actual: params.weekly;
                    }
                    agent {
                        docker {
                            image dockerImage()
                            args dockerArgs()
                            label "mlir && ${params.canXdlops ? '(gfx908 || gfx90a)' : 'rocm' }"
                            alwaysPull true
                        }
                    }
                    environment {
                        HOME="${WORKSPACE}"
                    }
                    stages {
                        stage('Environment') {
                            steps {
                                showEnv()
                            }
                        }
                        stage("Shared library build and fixed tests") {
                            steps {
                                buildProject('check-mlir check-mlir-rock', """
                                  -DMLIR_ROCK_DRIVER_PR_E2E_TEST_ENABLED=${params.nightly ? '0' : '1'}
                                  -DMLIR_ROCK_DRIVER_XDLOPS_TEST_ENABLED=${params.canXdlops ? '1' : '0'}
                                  -DMLIR_ROCK_DRIVER_E2E_TEST_ENABLED=${params.nightly ? '1' : '0'}
                                  -DROCK_E2E_TEST_ENABLED=${params.nightly ? '1' : '0'}
                                  -DMLIR_ROCK_DRIVER_MISC_E2E_TEST_ENABLED=${params.nightly ? '1' : '0'}
                                  -DMLIR_ROCK_DRIVER_TEST_GPU_VALIDATION=1
                                  -DLLVM_LIT_ARGS='-v --time-tests'
                                  -DCMAKE_EXPORT_COMPILE_COMMANDS=1
                                """)
                            }
                        }
                        stage('Static Tests') {
                            when { allOf {
                                equals expected: false, actual: params.nightly;
                                // Avoid this test running twice
                                equals expected: true, actual: params.canXdlops
                            } }
                            steps {
                               preMergeCheck()
                           }
                        }
                     }
                     post {
                        unsuccessful {
                            rebootNode()
                        }
                        always {
                            cleanWs()
                        }
                    }
                }
                stage("Shared Library: random E2E tests") {
                    when {
                        beforeAgent true;
                        allOf {
                            equals expected: true, actual: params.sharedLib;
                            equals expected: true, actual: params.nightly
                        }
                    }
                    agent {
                        docker {
                            image dockerImage()
                            args dockerArgs()
                            label "mlir && ${params.canXdlops ? '(gfx908 || gfx90a)' : 'rocm' }"
                            alwaysPull true
                        }
                    }
                    environment {
                        HOME="${WORKSPACE}"
                    }
                    stages {
                        stage('Environment') {
                            steps {
                                showEnv()
                            }
                        }
                        stage("Shared library build and random tests") {
                            steps {
                                buildProject('check-mlir-rock',
                                             """-DMLIR_ROCK_DRIVER_PR_E2E_TEST_ENABLED=0
                                             -DMLIR_ROCK_DRIVER_XDLOPS_TEST_ENABLED=${params.canXdlops ? '1' : '0'}
                                             -DMLIR_ROCK_DRIVER_E2E_TEST_ENABLED=1
                                             -DROCK_E2E_TEST_ENABLED=1
                                             -DMLIR_ROCK_DRIVER_RANDOM_DATA_SEED=1
                                             -DMLIR_ROCK_DRIVER_MISC_E2E_TEST_ENABLED=0
                                             -DMLIR_ROCK_DRIVER_TEST_GPU_VALIDATION=0
                                             -DLLVM_LIT_ARGS='-v --time-tests'
                                             -DCMAKE_EXPORT_COMPILE_COMMANDS=1""")
                            }
                        }
                    }
                    post {
                        unsuccessful {
                            rebootNode()
                        }
                        always {
                            cleanWs()
                        }
                    }
                }
                stage("Static Library PR tests") {
                    when {
                        beforeAgent true;
                        allOf {
                            equals expected: true, actual: params.staticLib;
                            equals expected: false, actual: params.nightly;
                            equals expected: false, actual: params.weekly;
                        }
                    }
                    agent {
                        docker {
                            image dockerImage()
                            args dockerArgs()
                            label "mlir && ${params.canXdlops ? '(gfx908 || gfx90a)' : 'gfx906' }"
                            alwaysPull true
                        }
                    }
                    environment {
                        PATH="/opt/rocm/llvm/bin:$PATH"
                        HOME="${WORKSPACE}"
                    }
                    stages {
                        stage('Environment') {
                            steps {
                                showEnv()
                            }
                        }
                        stage("Build Rock with librockCompiler") {
                            steps {
                                buildRockWithMLIR()
                            }
                        }
                        stage("Test selected Rock configs") {
                            environment {
                                // Make libRock.so accessible to the test driver
                                LD_LIBRARY_PATH="${WORKSPACE}/Rock/build/lib:$LD_LIBRARY_PATH:"
                            }
                            steps {
                                timeout(time: 60, activity: true, unit: 'MINUTES') {
                                    dir('Rock/build/') {
                                        // no-tuning test configs
                                        sh """
                                        bash ${WORKSPACE}/mlir/utils/jenkins/rock-tests/rock_validate.sh --test-all --no-tuning\
                                           < ${WORKSPACE}/mlir/utils/jenkins/rock-tests/selected-resnet50-rock-configs"""
                                        sh """
                                        bash ${WORKSPACE}/mlir/utils/jenkins/rock-tests/rock_validate.sh --test-fwd --dtype int8 --no-tuning\
                                           < ${WORKSPACE}/mlir/utils/jenkins/rock-tests/selected-resnet50-rock-configs"""
                                        // tuning test configs
                                        sh """
                                        bash ${WORKSPACE}/mlir/utils/jenkins/rock-tests/rock_validate.sh --test-all --tuning\
                                           < ${WORKSPACE}/mlir/utils/jenkins/rock-tests/selected-resnet50-rock-configs"""
                                        sh """
                                        bash ${WORKSPACE}/mlir/utils/jenkins/rock-tests/rock_validate.sh --test-fwd --dtype int8 --tuning\
                                           < ${WORKSPACE}/mlir/utils/jenkins/rock-tests/selected-resnet50-rock-configs"""
                                    }
                                }
                            }
                        }
                    }
                    post {
                        unsuccessful {
                            rebootNode()
                        }
                        always {
                            cleanWs()
                        }
                    }
                }
            }
        }
        stage ("Parameter sweeps") {
            when {
                beforeAgent true;
                equals expected: true, actual: params.weekly;
                equals expected: true, actual: params.sharedLib;
            }
            matrix {
                axes {
                    axis {
                        name 'CONFIG'
                        values 'conv_structure', 'perf_config'
                    }
                }
                agent {
                    docker {
                        image dockerImage()
                        args dockerArgs()
                        label "mlir && ${params.canXdlops ? '(gfx908 || gfx90a) && multi_gpu' : 'rocm' }"
                        alwaysPull true
                    }
                }
                environment {
                    HOME="${WORKSPACE}"
                }
                stages {
                    stage('Environment') {
                        steps {
                            showEnv()
                        }
                    }
                    stage("Sweep parameters") {
                        steps {
                            setHeartbeat()
                            buildProject(
                                'check-mlir-rock-build-only ci-performance-scripts',
                                '')
                            dir('build') {
                                timeout(time: 60, activity: true, unit: 'MINUTES') {
                                    sh """python3 ./bin/parameterSweeps.py \
                                        ${params.canXdlops ? '--xdlops' : '--no-xdlops'} \
                                        ${CONFIG}"""
                                }
                            }
                        }
                    }
                }
                post {
                    unsuccessful {
                        rebootNode()
                    }
                    always {
                        cleanWs()
                    }
                }
            }
        }
        stage ("Tune MLIR kernels") {
            when {
                beforeAgent true;
                equals expected: true, actual: params.weekly;
                equals expected: true, actual: params.staticLib;
            }
            matrix {
                axes {
                    axis {
                        name 'ARCH'
                        values 'gfx906', 'gfx908', 'gfx90a'
                    }
                }
                agent {
                    docker {
                        image dockerImage()
                        args dockerArgs()
                        label "mlir && ${ARCH}"
                        alwaysPull true
                    }
                }
                when {
                    beforeAgent true
                    anyOf {
                        allOf {
                            equals expected: "gfx906", actual: "${ARCH}";
                            equals expected: false, actual: params.canXdlops;
                        }
                        allOf {
                            anyOf {
                                equals expected: "gfx908", actual: "${ARCH}";
                                equals expected: "gfx90a", actual: "${ARCH}";
                            }
                            equals expected: true, actual: params.canXdlops;
                        }
                    }
                }
                environment {
                    PATH="/opt/rocm/llvm/bin:$PATH"
                    HOME="${WORKSPACE}"
                }
                stages {
                    stage("Set System Property on Lockhart nodes") {
                        when {
                            equals expected: "gfx90a", actual: "${ARCH}"
                        }
                        steps {
                            setHeartbeat()
                        }
                    }
                    stage("Environment") {
                        steps {
                            echo "ARCH = ${ARCH}"
                            showEnv()
                        }
                    }
                    stage("Build Rock with librockCompiler") {
                        steps {
                            buildRockWithMLIR()
                        }
                    }
                    stage("Tune MLIR kernels") {
                        environment {
                            // Make libRock.so accessible to the test driver
                            LD_LIBRARY_PATH="${WORKSPACE}/Rock/build/lib:$LD_LIBRARY_PATH:"
                        }
                        steps {
                            dir('Rock/build/') {
                                sh """
                                bash ${WORKSPACE}/mlir/utils/jenkins/rock-tests/rock_validate.sh --test-all --tuning\
                                    < ${WORKSPACE}/mlir/utils/jenkins/rock-tests/resnet50-rock-configs"""
                                sh """
                                bash ${WORKSPACE}/mlir/utils/jenkins/rock-tests/rock_validate.sh --test-fwd --dtype int8 --tuning\
                                    < ${WORKSPACE}/mlir/utils/jenkins/rock-tests/resnet50-rock-configs"""
                                sh 'ls -l RockUserDB'
                            }
                            // Save user database for nightly jobs
                            dir ('Rock/build/RockUserDB') {
                                stash name: "MLIR-PerfDB-${ARCH}", includes: "**"
                            }
                        }
                    }
                }
                post {
                    always {
                        cleanWs()
                    }
                }
            }
        }
        stage("Archive weekly xdlops tuning perfDB") {
            when {
                beforeAgent true;
                equals expected: true, actual: params.weekly;
                equals expected: true, actual: params.staticLib;
            }
            agent any
            options {
                skipDefaultCheckout()
            }
            steps {
                // Note: add additional architectures here
                dir ('Rock/build/RockUserDB') {
                    unstash name: "MLIR-PerfDB-${params.canXdlops ? 'gfx908' : 'gfx906'}"
                    // This'll stop being a duplicate once Navi CI starts
                    unstash name: "MLIR-PerfDB-${params.canXdlops ? 'gfx90a' : 'gfx906'}"
                    sh 'date --utc +%Y-%m-%d >tuning-date'
                }
                archiveArtifacts artifacts: 'Rock/build/RockUserDB/**',\
                    onlyIfSuccessful: true
            }
            post {
                always {
                    cleanWs()
                }
            }
        }

        stage("Rock Resnet50 Config Test") {
            when { allOf {
                    equals expected: true, actual: params.nightly;
                    equals expected: true, actual: params.staticLib;
            }}
            matrix {
                axes {
                    axis {
                        name 'DTYPE'
                        values 'fp16', 'fp32'
                    }
                    axis {
                        name 'LAYOUT'
                        values 'NCHW', 'NHWC'
                    }
                }
                stages {
                    stage('Test Rock integration') {
                        agent {
                            docker {
                                image dockerImage()
                                args dockerArgs()
                                label "mlir && ${params.canXdlops ? '(gfx908 || gfx90a)' : 'gfx906'}"
                                alwaysPull true
                            }
                        }
                        stages {
                            stage('Environment') {
                                steps {
                                    echo "Config is ($DTYPE, $LAYOUT)"
                                    showEnv()
                                }
                            }
                            stage("Build Rock with librockCompiler") {
                                environment {
                                    PATH="/opt/rocm/llvm/bin:$PATH"
                                    HOME="${WORKSPACE}"
                                }
                                steps {
                                    buildRockWithMLIR()
                                }
                            }
                            stage("Copy perfdb") {
                                steps {
                                    copyArtifacts filter: 'Rock/build/RockUserDB/**',\
                                               optional: true,\
                                               flatten: true,\
                                               projectName: "/MLIR/mlir-weekly",\
                                               selector: lastSuccessful(),\
                                               target: 'Rock/build/RockUserDB'
                                    sh 'ls Rock/build/RockUserDB'
                                    sh 'cat Rock/build/RockUserDB/tuning-date'
                                }
                            }
                            stage("Test Rock config") {
                                environment {
                                    // Make libRock.so accessible to the test driver
                                    LD_LIBRARY_PATH="${WORKSPACE}/Rock/build/lib:$LD_LIBRARY_PATH:"
                                    HOME="${WORKSPACE}"
                                }
                                steps {
                                    // Test with perfDb
                                    testRockDriver("$DTYPE", "$LAYOUT", "1", "0")
                                    testRockDriver("$DTYPE", "$LAYOUT", "2", "0")
                                    testRockDriver("$DTYPE", "$LAYOUT", "4", "0")
                                    // Test without perfDb
                                    sh 'rm -rf ${WORKSPACE}/Rock/build/RockUserDB'
                                    testRockDriver("$DTYPE", "$LAYOUT", "1", "0")
                                    testRockDriver("$DTYPE", "$LAYOUT", "2", "0")
                                    testRockDriver("$DTYPE", "$LAYOUT", "4", "0")
                                }
                            }
                        }
                        post {
                            unsuccessful {
                                rebootNode()
                            }
                            always {
                                cleanWs()
                            }
                        }
                    }
                }
            }
        }
        // FIXME: run perf tests on both gfx90a and gfx908
        stage("Benchmark and Report Performance") {
            when {
                beforeAgent true;
                equals expected: true, actual: params.perfTest;
                equals expected: true, actual: params.nightly;
            }
            agent {
                docker {
                    image dockerImage()
                    args dockerArgs()
                    label "mlir && ${params.canXdlops ? 'gfx90a' : 'gfx906' }"
                    alwaysPull true
                }
            }
            environment {
                // Make libRock.so accessible to the test driver
                PATH="/opt/rocm/llvm/bin:$PATH"
                LD_LIBRARY_PATH="${WORKSPACE}/Rock/build/lib:$LD_LIBRARY_PATH:"
                HOME="${WORKSPACE}"
            }
            stages {
                stage("Environment") {
                    steps {
                        showEnv()
                    }
                }
                stage("Build MLIR with librockCompiler") {
                    steps {
                        buildRockWithMLIR()
                    }
                }
                stage("Copy tuning database") {
                    steps {
                        copyArtifacts filter: 'Rock/build/RockUserDB/**',\
                                               optional: true,\
                                               flatten: true,\
                                               projectName: "/MLIR/mlir-weekly",\
                                               selector: lastSuccessful(),\
                                               target: 'Rock/build/RockUserDB'
                        sh 'ls Rock/build/RockUserDB'
                        sh 'cat Rock/build/RockUserDB/tuning-date'
                    }
                }
                stage("Performance Test: Tuned vs Untuned") {
                    steps {
                        buildProject('ci-performance-scripts', '')
                        dir('build') {
                           sh 'python3 ./bin/RockDriver.py -brock_use_tuned_mlir'
                           sh 'rm -rf ${WORKSPACE}/Rock/build/RockUserDB'
                           sh 'python3 ./bin/RockDriver.py -brock_use_untuned_mlir'
                        }
                    }
                }
                stage("Build Rock with HIP") {
                    steps {
                        sh 'rm -rf Rock'
                        dir('Rock') {
                            getAndBuildRock("--prefix ${WORKSPACE}/Rock/RockDeps",
                                '''-DROCK_BACKEND=HIP -DROCK_USE_MLIR=OFF
                                   -DCMAKE_PREFIX_PATH="${WORKSPACE}/Rock/RockDeps"
                                   -DCMAKE_INSTALL_PREFIX=${WORKSPACE}/Rock/build/RockInstallDir
                                   ''')
                            sh 'cd build; make -j $(nproc)'
                            sh 'cd build; make install'
                        }
                    }
                }
                stage("Build MLIR") {
                    steps {
                        // Clean up build settings to disable static library and allow
                        // mlir-rocm-runner
                        sh 'rm build/CMakeCache.txt'
                        buildProject('rock-gen mlir-rock-driver mlir-rocm-runner ci-performance-scripts', '')
                    }
                }
                stage("Test MLIR vs Rock") {
                    steps {
                        dir('build') {
                            sh 'date --utc +%Y-%m-%d > perf-run-date'
                            // Run MLIR perf benchmarks
                            sh 'python3 ./bin/RockDriver.py'
                        }
                    }
                }
                stage("Copy earlier performance results") {
                    steps {
                        copyArtifacts filter: 'build/*.csv,build/perf-run-date',\
                                               optional: true,\
                                               flatten: true,\
                                               projectName: "/${JOB_NAME}",\
                                               selector: lastSuccessful(),\
                                               target: 'build/oldData'
                    }
                }
                stage("Create performance reports") {
                    steps {
                        dir('build') {
                            sh 'ls -l'
                            sh 'python3 ./bin/createPerformanceReports.py'
                            sh 'python3 ./bin/perfRegressionReport.py'
                        }
                        postProcessPerfRes()
                    }
                }
            }
            post {
                unsuccessful {
                    rebootNode()
                }
                always {
                    cleanWs()
                }
             }
        }
    }
}
