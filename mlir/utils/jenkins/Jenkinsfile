// ON CHANGING THESE, ALSO CHANGE Jenkinsfile.downstream
void buildProject(String target, String cmakeOpts) {
    timeout(time: 60, activity: true, unit: 'MINUTES') {
        cmakeBuild generator: 'Ninja',\
            buildDir: 'build',\
            buildType: 'RelWithDebInfo',\
            installation: 'InSearchPath',\
            steps: [[args: target]],\
            cmakeArgs: "-DROCMLIR_DRIVER_ENABLED=1 $cmakeOpts"
    }
}

void buildMIOpen(String cmakeOpts) {
    sh '[ ! -d build ] || rm -rf build'
    cmakeBuild generator: 'Unix Makefiles',\
        buildDir: 'build',\
        buildType: 'Release',\
        installation: 'InSearchPath',\
        cmakeArgs: """-DCMAKE_CXX_COMPILER=/opt/rocm/llvm/bin/clang++
                     -DCMAKE_C_COMPILER=/opt/rocm/llvm/bin/clang
                     ${cmakeOpts}
                     """
    sh 'cd build; make -j $(nproc) MIOpenDriver'
}

void getAndBuildMIOpen(String prefixOpt, String cmakeOpts) {
    git branch: 'fix-kernel-count-MLIR', poll: false,\
        url: 'https://github.com/ROCmSoftwarePlatform/MIOpen.git'
    buildMIOpen(cmakeOpts)
}

void buildMIOpenWithMLIR() {
    buildProject('librockCompiler',
                    '-DBUILD_FAT_LIBROCKCOMPILER=ON')
    cmake arguments: "--install . --component librockCompiler --prefix ${WORKSPACE}/MIOpenDeps",\
        installation: 'InSearchPath', workingDir: 'build'

    dir('MIOpen') {
        git branch: 'fix-kernel-count-MLIR', poll: false,\
            url: 'https://github.com/ROCmSoftwarePlatform/MIOpen.git'
        // Note: setting cxxflags here works around https://github.com/ROCmSoftwarePlatform/MIOpen/issues/1604
        buildMIOpen("""-DMIOPEN_USE_MLIR=On
                        -DMIOPEN_BACKEND=HIP
                        -DCMAKE_PREFIX_PATH=${WORKSPACE}/MIOpenDeps
                        -DCMAKE_CXX_FLAGS="-isystem ${WORKSPACE}/MIOpenDeps/include"
                        -DMIOPEN_USER_DB_PATH=${WORKSPACE}/MIOpen/build/MIOpenUserDB
                        -DMIOPEN_TEST_FLAGS="--verbose --disable-verification-cache"
                        """)
    }
}

void testSelectedMIOpenConfigs() {
    dir('MIOpen/build/') {
        timeout(time: 60, activity: true, unit: 'MINUTES') {
         // no-tuning test configs
         sh """
         bash ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/miopen_validate.sh --test-all --no-tuning\
            < ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/selected-resnet50-miopen-configs"""
         sh """
         bash ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/miopen_validate.sh --test-fwd --dtype int8 --no-tuning\
            < ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/selected-resnet50-miopen-configs"""
         // tuning test configs
         sh """
         bash ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/miopen_validate.sh --test-all --tuning\
            < ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/selected-resnet50-miopen-configs"""
         sh """
         bash ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/miopen_validate.sh --test-fwd --dtype int8 --tuning\
            < ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/selected-resnet50-miopen-configs"""
         }
     }
}

void showEnv() {
    echo "$env.NODE_NAME"
    sh 'cat /etc/os-release'
    sh 'ulimit -a'
    // Ignore rocm-smi failures in ixt-sjc2-05
    sh '/opt/rocm/bin/rocm-smi || true'
    sh '/opt/rocm/bin/rocm_agent_enumerator'
    sh 'id'
}

String dockerArgs() {
    return "--device=/dev/kfd --device=/dev/dri --group-add video --group-add render -v /etc/passwd:/etc/passwd:ro -v /etc/group:/etc/group:ro"
}

String dockerImage() {
    return 'rocm/mlir:rocm5.2-latest'
}

void preMergeCheck() {
    sh '''
    if [ ! -f ./build/compile_commands.json ];  then
      echo "No compile commands, bailing."
      exit 1
    fi
    if [ ! -f ./compile_commands.json ]; then
      ln -s build/compile_commands.json compile_commands.json
    fi
    '''
    sh 'python3 ./mlir/utils/jenkins/static-checks/premerge-checks.py'
}

void testMIOpenDriver(String dtype, String layout, String direction, String tuning) {
    echo "Config is (${dtype}, ${layout}, dir=${direction}, tuning=${tuning})"
    timeout(time: 60, activity: true, unit: 'MINUTES') {
        dir('MIOpen/build/') {
            sh """
            bash ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/miopen_validate.sh \
            --layout ${layout} \
            --direction ${direction} \
            --dtype ${dtype} \
            ${tuning == '1' ? '--tuning' : '--no-tuning'} \
            < ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/resnet50-miopen-configs"""
        }
    }
}

void postProcessPerfRes(String chip) {
    publishHTML (target: [
        allowMissing: false,
        alwaysLinkToLastBuild: false,
        keepAll: true,
        reportDir: 'build',
        reportFiles: "${chip}_MLIR_Performance_Changes.html,${chip}_MLIR_vs_MIOpen.html",
        reportName: "Performance report for ${chip}"
    ])

    plot csvFileName: "${chip}_plot-nightly-perf-results-000001.csv",\
        csvSeries: [[file: "build/${chip}_mlir_vs_miopen_perf_for_plot.csv", displayTableFlag: false]],\
        title: 'Test performance summary',\
        yaxis: 'TFlops',\
        style: 'line',\
        group: 'Performance plots'

    // Save results for future comparison
    archiveArtifacts artifacts: 'build/*_mlir_*.csv,build/perf-run-date', onlyIfSuccessful: true
}

//makes sure multiple builds are not triggered for branch indexing
def resetBuild() {
    if (currentBuild.getPreviousBuild() == null
        || currentBuild.getPreviousBuild().getBuildCauses().toString().contains('BranchIndexingCause')) {
        def buildNumber = BUILD_NUMBER as int;
        if (buildNumber > 1)
            milestone(buildNumber - 1);
        milestone(buildNumber)
    }
}

void setHeartbeat() {
    script {
        System.setProperty("org.jenkinsci.plugins.durabletask.BourneShellScript.HEARTBEAT_CHECK_INTERVAL", "86400");
    }
}

String getLabelFromCodepath(String codepath) {
    echo "codepath is ${codepath}"
    if (codepath == "mfma") {
        label = 'mlir && (gfx908 || gfx90a)'
    } else if (codepath == "navi21") {
        label = 'mlir && gfx1030'
    } else if (codepath == "vanilla"){
        label = 'mlir && gfx906'
    } else {
        echo "${codepath} is not supported"
        label = 'wrongLabel'
    }
    echo "label is ${label}"
    return label
}

def rebootNode() {
    build job: 'maintenance/reboot-slaves', propagate: false , parameters: [string(name: 'server', value: "${env.NODE_NAME}"),]
}

pipeline {
    agent none
    parameters {
        // Below should be set statically by Jenkins job
        booleanParam(name: 'nightly', defaultValue: params.nightly ? true : false,
                     description: 'Run extra nightly-only tests')
        booleanParam(name: 'canXdlops', defaultValue: params.canXdlops == false ? false : true,
                     description: 'Can this CI instance use xdlops (no for public server)')
        booleanParam(name: 'weekly', defaultValue: params.weekly ? true : false,
                     description: 'Run weekly-only jobs')

        // Each below control whether to run a individual stage from parallel run
        // They default to true or empty but deverloper can toggle them for debugging purpose
        booleanParam(name: 'sharedLib', defaultValue: true,
                     description: 'Run the shared library stage')
        booleanParam(name: 'staticLib', defaultValue: true,
                     description: 'Run the static library stage')
        booleanParam(name: 'perfTest', defaultValue: true,
                     description: 'Run the performance testing stage')

        // choose the codepath for testing
        choice(name: 'codepath',
               choices: ['default', 'mfma', 'navi21', 'vanilla'],
               description: 'Choose the codepath to test')
        // option to disable navi21 cells in case nodes are offline
        booleanParam(name: 'disableNavi21', defaultValue: false,
                     description: 'Disable testing on Navi21')
    }
    stages {
        stage("Set System Property") {
            steps {
                setHeartbeat()
            }
        }
        stage("Kill old PR builds") {
            when {
                equals expected: false, actual: params.weekly;
                equals expected: false, actual: params.nightly;
            }
            steps {
                resetBuild()
            }
        }
        stage("Build and Test") {
            when {
                beforeAgent true
                // Skip this part of testing as early as possible
                equals expected: false, actual: params.weekly;
            }
            matrix {
                axes {
                    axis {
                        name 'CODEPATH'
                        values 'vanilla', 'mfma', 'navi21'
                    }
                }
                agent {
                    docker {
                        image dockerImage()
                        args dockerArgs()
                        label getLabelFromCodepath("${CODEPATH}")
                        alwaysPull true
                    }
                }
                when {
                    beforeAgent true
                    anyOf {
                        // When default codepath is selected, we test mfma and navi21 on private CI
                        // and vanilla on public CI
                        allOf {
                            equals expected: "default", actual: params.codepath;
                            anyOf {
                                allOf {
                                    anyOf {
                                        equals expected: "mfma", actual: "${CODEPATH}";
                                        allOf {
                                            equals expected: "navi21", actual: "${CODEPATH}";
                                            equals expected: false, actual: params.disableNavi21;
                                        }
                                    }
                                    equals expected: true, actual: params.canXdlops;
                                }
                                allOf {
                                    equals expected: "vanilla", actual: "${CODEPATH}";
                                    equals expected: false, actual: params.canXdlops;
                                }
                            }
                        }
                        // When a particular codepath is selected, we only test the codepath
                        // on private CI
                        allOf {
                            anyOf {
                                equals expected: "mfma", actual: params.codepath;
                                allOf {
                                    equals expected: "navi21", actual: params.codepath;
                                    equals expected: false, actual: params.disableNavi21;
                                }
                                equals expected: "vanilla", actual: params.codepath;
                            }
                            equals expected: "${CODEPATH}", actual: params.codepath;
                            equals expected: true, actual: params.canXdlops;
                        }
                    }
                }
                environment {
                    PATH="/opt/rocm/llvm/bin:$PATH"
                    HOME="${WORKSPACE}"
                }
                stages {
                    stage('Environment') {
                        steps {
                            showEnv()
                        }
                    }
                    stage("Shared Library: fixed E2E tests") {
                        when {
                            beforeAgent true;
                            equals expected: true, actual: params.sharedLib;
                        }
                        steps {
                            buildProject('check-mlir check-rocmlir', """
                                      -DROCMLIR_DRIVER_PR_E2E_TEST_ENABLED=${params.nightly ? '0' : '1'}
                                      -DROCMLIR_DRIVER_E2E_TEST_ENABLED=${params.nightly ? '1' : '0'}
                                      -DROCK_E2E_TEST_ENABLED=${params.nightly ? '1' : '0'}
                                      -DROCMLIR_DRIVER_MISC_E2E_TEST_ENABLED=${params.nightly ? '1' : '0'}
                                      -DROCMLIR_DRIVER_TEST_GPU_VALIDATION=1
                                      -DLLVM_LIT_ARGS='-v --time-tests'
                                      -DCMAKE_EXPORT_COMPILE_COMMANDS=1
                                    """)
                        }
                    }
                    stage("Shared Library: random E2E tests") {
                        when {
                            beforeAgent true;
                            allOf {
                                equals expected: true, actual: params.sharedLib;
                                equals expected: true, actual: params.nightly
                            }
                        }
                        steps {
                            buildProject('check-rocmlir', """
                                      -DROCMLIR_DRIVER_PR_E2E_TEST_ENABLED=0
                                      -DROCMLIR_DRIVER_E2E_TEST_ENABLED=1
                                      -DROCK_E2E_TEST_ENABLED=1
                                      -DROCMLIR_DRIVER_RANDOM_DATA_SEED=1
                                      -DROCMLIR_DRIVER_MISC_E2E_TEST_ENABLED=0
                                      -DROCMLIR_DRIVER_TEST_GPU_VALIDATION=0
                                      -DLLVM_LIT_ARGS='-v --time-tests'
                                      -DCMAKE_EXPORT_COMPILE_COMMANDS=1
                                    """)
                        }
                    }
                    stage("Static Library: test librockCompiler with MIOpen") {
                        when {
                            beforeAgent true;
                            equals expected: true, actual: params.staticLib;
                            equals expected: false, actual: params.nightly;
                        }
                        environment {
                            // Make libMIOpen.so accessible to the test driver
                            LD_LIBRARY_PATH="${WORKSPACE}/MIOpen/build/lib:$LD_LIBRARY_PATH:"
                        }
                        steps {
                            sh 'rm build/CMakeCache.txt'
                            buildMIOpenWithMLIR()
                            testSelectedMIOpenConfigs()
                        }
                    }
                    stage('Static Tests') {
                        when {
                            allOf {
                                equals expected: false, actual: params.nightly;
                                // Avoid this test running multiple times
                                equals expected: "mfma", actual: "${CODEPATH}"
                            }
                        }
                        steps {
                            preMergeCheck()
                        }
                    }
                }
                post {
                    unsuccessful {
                        rebootNode()
                    }
                    always {
                        cleanWs()
                    }
                }
            }
        }
        stage ("Parameter sweeps") {
            when {
                beforeAgent true;
                equals expected: true, actual: params.weekly;
                equals expected: true, actual: params.sharedLib;
            }
            matrix {
                axes {
                    axis {
                        name 'CONFIG'
                        values 'conv_structure', 'perf_config'
                    }
                }
                agent {
                    docker {
                        image dockerImage()
                        args dockerArgs()
                        label "mlir && ${params.canXdlops ? '(gfx908 || gfx90a) && multi_gpu' : 'rocm' }"
                        alwaysPull true
                    }
                }
                environment {
                    HOME="${WORKSPACE}"
                }
                stages {
                    stage('Environment') {
                        steps {
                            showEnv()
                        }
                    }
                    stage("Sweep parameters") {
                        steps {
                            setHeartbeat()
                            buildProject(
                                'check-rocmlir-build-only ci-performance-scripts',
                                '')
                            dir('build') {
                                timeout(time: 60, activity: true, unit: 'MINUTES') {
                                    sh """python3 ./bin/parameterSweeps.py \
                                        ${params.canXdlops ? '--xdlops' : '--no-xdlops'} \
                                        ${CONFIG}"""
                                }
                            }
                        }
                    }
                }
                post {
                    unsuccessful {
                        rebootNode()
                    }
                    always {
                        cleanWs()
                    }
                }
            }
        }
        stage ("Tune MLIR kernels") {
            when {
                beforeAgent true;
                equals expected: true, actual: params.weekly;
                equals expected: true, actual: params.staticLib;
            }
            matrix {
                axes {
                    axis {
                        name 'ARCH'
                        values 'gfx906', 'gfx908', 'gfx90a', 'gfx1030'
                    }
                }
                agent {
                    docker {
                        image dockerImage()
                        args dockerArgs()
                        label "mlir && ${ARCH}"
                        alwaysPull true
                    }
                }
                when {
                    beforeAgent true
                    anyOf {
                        allOf {
                            equals expected: "gfx906", actual: "${ARCH}";
                            equals expected: false, actual: params.canXdlops;
                        }
                        allOf {
                            anyOf {
                                equals expected: "gfx908", actual: "${ARCH}";
                                equals expected: "gfx90a", actual: "${ARCH}";
                                equals expected: "gfx1030", actual: "${ARCH}";
                            }
                            equals expected: true, actual: params.canXdlops;
                        }
                    }
                }
                environment {
                    PATH="/opt/rocm/llvm/bin:$PATH"
                    HOME="${WORKSPACE}"
                }
                stages {
                    stage("Set System Property on Lockhart nodes") {
                        when {
                            equals expected: "gfx90a", actual: "${ARCH}"
                        }
                        steps {
                            setHeartbeat()
                        }
                    }
                    stage("Environment") {
                        steps {
                            echo "ARCH = ${ARCH}"
                            showEnv()
                        }
                    }
                    stage("Build MIOpen with librockCompiler") {
                        steps {
                            buildMIOpenWithMLIR()
                        }
                    }
                    stage("Tune MLIR kernels") {
                        environment {
                            // Make libMIOpen.so accessible to the test driver
                            LD_LIBRARY_PATH="${WORKSPACE}/MIOpen/build/lib:$LD_LIBRARY_PATH:"
                        }
                        steps {
                            dir('MIOpen/build/') {
                                sh """
                                bash ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/miopen_validate.sh --test-all --tuning\
                                    < ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/resnet50-miopen-configs"""
                                sh """
                                bash ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/miopen_validate.sh --test-fwd --dtype int8 --tuning\
                                    < ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/resnet50-miopen-configs"""
                                sh 'ls -l MIOpenUserDB'
                            }
                            // Save user database for nightly jobs
                            dir ('MIOpen/build/MIOpenUserDB') {
                                stash name: "MLIR-PerfDB-${ARCH}", includes: "**"
                            }
                        }
                    }
                }
                post {
                    always {
                        cleanWs()
                    }
                }
            }
        }
        stage("Archive weekly xdlops tuning perfDB") {
            when {
                beforeAgent true;
                equals expected: true, actual: params.weekly;
                equals expected: true, actual: params.staticLib;
            }
            agent any
            options {
                skipDefaultCheckout()
            }
            steps {
                // Note: add additional architectures here
                dir ('MIOpen/build/MIOpenUserDB') {
                    unstash name: "MLIR-PerfDB-${params.canXdlops ? 'gfx908' : 'gfx906'}"
                    // This'll stop being a duplicate once Navi CI starts
                    unstash name: "MLIR-PerfDB-${params.canXdlops ? 'gfx90a' : 'gfx906'}"
                    sh 'date --utc +%Y-%m-%d >tuning-date'
                }
                archiveArtifacts artifacts: 'MIOpen/build/MIOpenUserDB/**',\
                    onlyIfSuccessful: true
            }
            post {
                always {
                    cleanWs()
                }
            }
        }

        stage("MIOpen Resnet50 Config Test") {
            when { allOf {
                    equals expected: true, actual: params.nightly;
                    equals expected: true, actual: params.staticLib;
            }}
            matrix {
                axes {
                    axis {
                        name 'DTYPE'
                        values 'fp16', 'fp32'
                    }
                    axis {
                        name 'LAYOUT'
                        values 'NCHW', 'NHWC'
                    }
                }
                stages {
                    stage('Test MIOpen integration') {
                        agent {
                            docker {
                                image dockerImage()
                                args dockerArgs()
                                label "mlir && ${params.canXdlops ? '(gfx908 || gfx90a)' : 'gfx906'}"
                                alwaysPull true
                            }
                        }
                        stages {
                            stage('Environment') {
                                steps {
                                    echo "Config is ($DTYPE, $LAYOUT)"
                                    showEnv()
                                }
                            }
                            stage("Build MIOpen with librockCompiler") {
                                environment {
                                    PATH="/opt/rocm/llvm/bin:$PATH"
                                    HOME="${WORKSPACE}"
                                }
                                steps {
                                    buildMIOpenWithMLIR()
                                }
                            }
                            stage("Copy perfdb") {
                                steps {
                                    copyArtifacts filter: 'MIOpen/build/MIOpenUserDB/**',\
                                               optional: true,\
                                               flatten: true,\
                                               projectName: "/MLIR/mlir-weekly",\
                                               selector: lastSuccessful(),\
                                               target: 'MIOpen/build/MIOpenUserDB'
                                    sh 'ls MIOpen/build/MIOpenUserDB'
                                    sh 'cat MIOpen/build/MIOpenUserDB/tuning-date'
                                }
                            }
                            stage("Test MIOpen config") {
                                environment {
                                    // Make libMIOpen.so accessible to the test driver
                                    LD_LIBRARY_PATH="${WORKSPACE}/MIOpen/build/lib:$LD_LIBRARY_PATH:"
                                    HOME="${WORKSPACE}"
                                }
                                steps {
                                    // Test with perfDb
                                    testMIOpenDriver("$DTYPE", "$LAYOUT", "1", "0")
                                    testMIOpenDriver("$DTYPE", "$LAYOUT", "2", "0")
                                    testMIOpenDriver("$DTYPE", "$LAYOUT", "4", "0")
                                    // Test without perfDb
                                    sh 'rm -rf ${WORKSPACE}/MIOpen/build/MIOpenUserDB'
                                    testMIOpenDriver("$DTYPE", "$LAYOUT", "1", "0")
                                    testMIOpenDriver("$DTYPE", "$LAYOUT", "2", "0")
                                    testMIOpenDriver("$DTYPE", "$LAYOUT", "4", "0")
                                }
                            }
                        }
                        post {
                            unsuccessful {
                                rebootNode()
                            }
                            always {
                                cleanWs()
                            }
                        }
                    }
                }
            }
        }
        // FIXME: run perf tests on both gfx90a and gfx908
        stage("Benchmark and Report Performance") {
            matrix {
                axes {
                    axis {
                        name 'CHIP'
                        values 'gfx906', 'gfx908', 'gfx90a', 'gfx1030'
                    }
                }
                when {
                    beforeAgent true;
                    equals expected: true, actual: params.perfTest;
                    equals expected: true, actual: params.nightly;
                    anyOf {
                        allOf {
                            anyOf {
                                equals expected: "gfx908", actual: "${CHIP}";
                                equals expected: "gfx90a", actual: "${CHIP}";
                                allOf {
                                    equals expected: "gfx1030", actual: "${CHIP}";
                                    equals expected: false, actual: params.disableNavi21;
                                }
                            }
                            equals expected: true, actual: params.canXdlops;
                        }
                        allOf {
                            equals expected: "gfx906", actual: "${CHIP}";
                            equals expected: false, actual: params.canXdlops;
                        }
                    }
                }
                agent {
                    docker {
                        image dockerImage()
                        args dockerArgs()
                        label "${CHIP}"
                        alwaysPull true
                    }
                }
                environment {
                    // Make libMIOpen.so accessible to the test driver
                    PATH="/opt/rocm/llvm/bin:$PATH"
                    LD_LIBRARY_PATH="${WORKSPACE}/MIOpen/build/lib:$LD_LIBRARY_PATH:"
                    HOME="${WORKSPACE}"
                }
                stages {
                    stage("Environment") {
                        steps {
                            showEnv()
                        }
                    }
                    stage("Build MLIR with librockCompiler") {
                        steps {
                            buildMIOpenWithMLIR()
                        }
                    }
                    stage("Copy tuning database") {
                        steps {
                            copyArtifacts filter: 'MIOpen/build/MIOpenUserDB/**',\
                                optional: true,\
                                flatten: true,\
                                projectName: "/MLIR/mlir-weekly",\
                                selector: lastSuccessful(),\
                                target: 'MIOpen/build/MIOpenUserDB'
                                sh 'ls MIOpen/build/MIOpenUserDB'
                                sh 'cat MIOpen/build/MIOpenUserDB/tuning-date'
                        }
                    }
                    stage("Performance Test: Tuned vs Untuned") {
                        steps {
                            buildProject('ci-performance-scripts', '')
                            dir('build') {
                                sh 'python3 ./bin/MIOpenDriver.py --miopen_use_tuned_mlir'
                                sh 'rm -rf ${WORKSPACE}/MIOpen/build/MIOpenUserDB'
                                sh 'python3 ./bin/MIOpenDriver.py --miopen_use_untuned_mlir'
                            }
                        }
                    }
                    stage("Build MIOpen with HIP") {
                        steps {
                            sh 'rm -rf MIOpen'
                            dir('MIOpen') {
                                getAndBuildMIOpen("--prefix ${WORKSPACE}/MIOpen/MIOpenDeps", '''
                                    -DMIOPEN_BACKEND=HIP -DMIOPEN_USE_MLIR=OFF
                                    -DCMAKE_PREFIX_PATH="${WORKSPACE}/MIOpen/MIOpenDeps"
                                    -DCMAKE_INSTALL_PREFIX=${WORKSPACE}/MIOpen/build/MIOpenInstallDir
                                    ''')
                                sh 'cd build; make -j $(nproc)'
                                sh 'cd build; make install'
                            }
                        }
                    }
                    stage("Build MLIR") {
                        steps {
                            // Clean up build settings to disable static library and allow
                            // mlir-rocm-runner
                            sh 'rm build/CMakeCache.txt'
                            buildProject('rocmlir-gen rocmlir-driver mlir-rocm-runner ci-performance-scripts', '')
                        }
                    }
                    stage("Test MLIR vs MIOpen") {
                        steps {
                            dir('build') {
                                sh 'date --utc +%Y-%m-%d > perf-run-date'
                                // Run MLIR perf benchmarks
                                sh 'python3 ./bin/MIOpenDriver.py'
                            }
                        }
                    }
                    stage("Copy earlier performance results") {
                        steps {
                            copyArtifacts filter: 'build/*.csv,build/perf-run-date',\
                                optional: true,\
                                flatten: true,\
                                projectName: "/${JOB_NAME}",\
                                selector: lastSuccessful(),\
                                target: 'build/oldData'
                        }
                    }
                    stage("Create performance reports") {
                        steps {
                            dir('build') {
                                sh 'ls -l'
                                sh 'python3 ./bin/createPerformanceReports.py ${CHIP}'
                                sh 'python3 ./bin/perfRegressionReport.py ${CHIP}'
                            }
                            postProcessPerfRes("${CHIP}")
                        }
                    }
                }
                post {
                    unsuccessful {
                        rebootNode()
                    }
                    always {
                        cleanWs()
                    }
                }
            }
        }
    }
}
