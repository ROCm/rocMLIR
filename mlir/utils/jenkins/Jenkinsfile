void buildProject(String target, String cmakeOpts) {
    cmakeBuild generator: 'Ninja',\
        buildDir: 'build',\
        buildType: 'Release',\
        installation: 'InSearchPath',\
        steps: [[args: target]],\
        cmakeArgs: "-DMLIR_MIOPEN_DRIVER_ENABLED=1 $cmakeOpts"
}

void getAndBuildMIOpen(String cmakeOpts) {
    dir('build') {
        deleteDir()
    }
    git branch: 'develop', poll: false,\
        url: 'https://github.com/ROCmSoftwarePlatform/MIOpen.git'
    cmake arguments: "-P install_deps.cmake --minimum",\
        installation: "InSearchPath"
    cmakeBuild generator: 'Ninja',\
        buildDir: 'build',\
        buildType: 'Release',\
        installation: 'InSearchPath',\
        steps: [[args: "MIOpenDriver"]],\
        cmakeArgs: """-DCMAKE_CXX_COMPILER=/opt/rocm/llvm/bin/clang++
                     -DCMAKE_C_COMPILER=/opt/rocm/llvm/bin/clang
                     ${cmakeOpts}
                     """
}

void showEnv() {
    sh 'cat /etc/os-release'
    sh '/opt/rocm/bin/rocm-smi'
}

String dockerArgs() {
    return '--user "$(id -u):$(id -g)" --device=/dev/kfd --device=/dev/dri --group-add video -u 0'
}

String dockerImage() {
    return 'rocm/mlir:rocm4.1-latest'
}

pipeline {
    agent none
    parameters {
        booleanParam(name: 'nightly', defaultValue: params.nightly ?: false,
                     description: 'Run extra nightly-only tests')
        booleanParam(name: 'canXdlops', defaultValue: params.canXdlops ?: false,
                     description: 'Can this CI instance use xdlops (no for public server)')
    }
    stages {
        stage("Builds and tests") {
            parallel {
                stage("Shared library") {
                    agent {
                        docker {
                            image dockerImage()
                            args dockerArgs()
                            label "${params.canXdlops ? 'gfx908' : 'rocm' } && multi_gpu"
                            alwaysPull true
                        }
                    }
                    environment {
                        PATH="$PATH:/opt/rocm/llvm/bin"
                    }
                    stages {
                        stage('Environment') {
                            steps {
                                showEnv()
                            }
                        }
                        stage("Shared library build and test") {
                            steps {
                                buildProject('check-mlir', """
                               -DMLIR_MIOPEN_DRIVER_PR_E2E_TEST_ENABLED=${params.nightly ? '0' : '1'}
                               -DMLIR_MIOPEN_DRIVER_XDLOPS_TEST_ENABLED=${params.canXdlops ? '1' : '0'}
                               -DMLIR_MIOPEN_DRIVER_E2E_TEST_ENABLED=${params.nightly ? '1' : '0'}
                               -DMLIR_MIOPEN_DRIVER_MISC_E2E_TEST_ENABLED=${params.nightly ? '1' : '0'}
                               -DCMAKE_EXPORT_COMPILE_COMMANDS=1
                               """)
                            }
                        }
                        stage('Static Tests') {
                            when { allOf {
                                equals expected: false, actual: params.nightly;
                                // Avoid this test running twice
                                equals expected: false, actual: params.canXdlops
                            } }
                            steps {
                                sh '''
                                if [ ! -f ./build/compile_commands.json ];  then
                                  echo "No compile commands, bailing."
                                  exit 1
                                fi
                                if [ ! -f ./compile_commands.json ]; then
                                  ln -s build/compile_commands.json compile_commands.json
                                fi
                                '''
                                sh 'python3 ./mlir/utils/jenkins/static-checks/premerge-checks.py'
                            }
                        }
                        stage("Random E2E test") {
                            when {
                                equals expected: true, actual: params.nightly
                            }
                            // Reuses previous build config
                            steps {
                                buildProject('check-mlir',
                                             """-DMLIR_MIOPEN_DRIVER_RANDOM_DATA_SEED=1
                                             -DMLIR_MIOPEN_DRIVER_MISC_E2E_TEST_ENABLED=0""")
                            }
                        }
                    }
                    post {
                        always {
                            cleanWs()
                        }
                    }
                }

                stage("Implicit GEMM") {
                    agent {
                        docker {
                            image dockerImage()
                            args dockerArgs()
                            label 'rocm'
                            alwaysPull true
                        }
                    }
                    environment {
                        PATH="$PATH:/opt/rocm/llvm/bin"
                    }
                    stages {
                        stage("Igemm build") {
                            steps {
                                checkout scm
                                buildProject('libMLIRMIOpen',
                                             '-DBUILD_FAT_LIBMLIRMIOPEN=ON')
                            }
                        }

                        stage("Install libMLIRMIOpen") {
                            when { allOf {
                                equals expected: true, actual: params.nightly;
                                equals expected: true, actual: params.canXdlops
                            }}
                            steps {
                                cmake arguments: '--install . --component libMLIRMIOpen --prefix /opt/rocm',\
                                installation: 'InSearchPath', workingDir: 'build'
                            }
                        }
                        stage("Build and stash MIOpen") {
                            when { allOf {
                                equals expected: true, actual: params.nightly;
                                equals expected: true, actual: params.canXdlops
                            }}
                            steps {
                                dir('MIOpen') {
                                        getAndBuildMIOpen('''-DMIOPEN_USE_MLIR=On
                                        -DBUILD_DEV=On
                                        -DMIOPEN_TEST_FLAGS=\'--verbose --disable-verification-cache\'
                                        ''')
                                }
                                stash name:"MIOpen-test-requisites",\
                                    includes: """MIOpen/src/kernels/**,\
MIOpen/build/bin/MIOpenDriver,\
build/lib/*,\
mlir/utils/jenkins/miopen-tests/**\
"""
                            }
                        }
                    }
                    post {
                        always {
                            cleanWs()
                        }
                    }
                }

                stage("Performance testing") {
                    when {
                        beforeAgent true;
                        // Does this only run on private CI?
                        equals expected: true, actual: params.nightly
                    }
                    agent {
                        docker {
                            image dockerImage()
                            args dockerArgs()
                            label 'mlir'
                            alwaysPull true
                        }
                    }
                    environment {
                        PATH="$PATH:/opt/rocm/llvm/bin"
                    }
                    stages {
                        stage("Environment") {
                            steps {
                                showEnv()
                            }
                        }
                        stage("Build MIOpen with HIP") {
                            steps {
                                dir('MIOpen') {
                                    getAndBuildMIOpen('''-DMIOPEN_BACKEND=HIP
                                    -DCMAKE_PREFIX_PATH="/opt/rocm/hip;/opt/rocm/;/usr/local"
                                    -DCMAKE_INSTALL_PREFIX=/opt/rocm/
                                    ''')
                                }
                            }
                        }
                        stage("Build MLIR") {
                            buildProject('mlir-miopen-driver mlir-rocm-runner', '')
                        }
                        stage('Install PrettyTable (temporary)') {
                            sh 'pip3 install PrettyTable'
                        }
                        stage('Performance Test') {
                            steps {
                                cmake arguments: '--build . MIOpenDriver.py',\
                                    installation: 'InSearchPath', workingDir: 'build'
                                dir('build') {
                                    // Run MLIR perf benchmarks
                                    sh 'python3 ./llvm/MIOpenDriver.py'
                                }
                                publishHTML (target: [
                                    allowMissing: false,
                                    alwaysLinkToLastBuild: false,
                                    keepAll: true,
                                    reportDir: 'build',
                                    reportFiles: 'MLIR_vs_MIOpen.html',
                                    reportName: "MLIR vs MIOpen performance"
                                ])
                            }
                        }
                    }
                    post {
                        always {
                            dir('build') {
                                deleteDir()
                            }
                        }
                    }
                }
                // We're apparently not running this test these days
                // stage("Upstream build") {
                //     when { beforeAgent true;
                //           allOf {
                //             equals expected: true, actual: params.nightly;
                //             equals expected: false, actual: params.canXdlops
                //         }}
                //     agent {
                //             docker {
                //             image dockerImage()
                //             args dockerArgs()
                //             label 'rocm'
                //             alwaysPull true
                //         }
                //     }
                //     options {
                //         skipDefaultCheckout()
                //     }
                //     steps {
                //         git branch: 'main', poll: false,\
                //             url: 'https://github.com/llvm/llvm-project'
                //         cmakeBuild generator: 'Ninja',\
                //             buildDir: 'build',\
                //             sourceDir: 'llvm',\
                //             buildType: 'Release',\
                //             installation: 'InSearchPath',\
                //             steps: [[args: 'check-mlir']],\
                //             // Reminder to keep these up to date with our cmake
                //             cmakeArgs: '''
                //             -DLLVM_ENABLE_PROJECTS="mlir;lld"
                //             -DLLVM_BUILD_EXAMPLES=ON
                //             -DLLVM_TARGETS_TO_BUILD="X86;AMDGPU"
                //             -DLLVM_ENABLE_ASSERTIONS=ON
                //             -DBUILD_SHARED_LIBS=ON
                //             -DLLVM_BUILD_LLVM_DYLIB=ON
                //             -DMLIR_ROCM_RUNNER_ENABLED=1
                //             '''
                //     }
                //     post {
                //         always {
                //             cleanWs()
                //         }
                //     }
                // }
            }
        }

        stage("MIOpen resnet50 config test") {
            when {
                equals expected: true, actual: params.nightly
            }
            matrix {
                agent {
                    docker {
                        image dockerImage()
                        args dockerArgs()
                        label "${XDLOPS == '1' ? 'gfx908' : 'gfx908'} && multi_gpu"
                        alwaysPull true
                    }
                }
                options {
                    skipDefaultCheckout()
                }
                axes {
                    axis {
                        name 'DTYPE'
                        values 'fp16', 'fp32'
                    }
                    axis {
                        name 'XDLOPS'
                        values '1', '0'
                    }
                    axis {
                        name 'LAYOUT'
                        values 'NCHW', 'NHWC'
                    }
                    axis {
                        name 'DIRECTION'
                        values '1', '4'
                    }
                }
                stages {
                    stage('Environment') {
                        steps {
                            echo "Config is ($DTYPE, $LAYOUT, xdlops=$XDLOPS, dir=$DIRECTION)"
                            showEnv()
                        }
                    }
                    stage("Copy MIOpen test environment") {
                        steps {
                            unstash name: "MIOpen-test-requisites"
                        }
                    }
                    stage("Test MIOpen config") {
                        steps {
                            echo "Config is ($DTYPE, $LAYOUT, xdlops=$XDLOPS, dir=$DIRECTION)"
                            dir('MIOpen/build/') {
                                sh """
bash ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/miopen_validate.sh \
--layout $LAYOUT \
--direction $DIRECTION \
--dtype $DTYPE \
${XDLOPS == '1' ? '--xdlops' : '--no-xdlops'} <${WORKSPACE}/mlir/utils/jenkins/miopen-tests/resnet50-miopen-configs"""
                            }
                        }
                    }
                }
                post {
                    always {
                        cleanWs()
                    }
                }
            }
        }
    }
}
