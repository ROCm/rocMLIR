// ON CHANGING THESE, ALSO CHANGE Jenkinsfile.downstream
void buildProject(String target, String cmakeOpts) {
    cmakeBuild generator: 'Ninja',\
        buildDir: 'build',\
        buildType: 'RelWithDebInfo',\
        installation: 'InSearchPath',\
        steps: [[args: target]],\
        cmakeArgs: "-DMLIR_MIOPEN_DRIVER_ENABLED=1 $cmakeOpts"
}

void getAndBuildMIOpen(String prefixOpt, String cmakeOpts) {
    git branch: 'develop', poll: false,\
        url: 'https://github.com/ROCmSoftwarePlatform/MIOpen.git'
    cmake arguments: "-P install_deps.cmake --minimum ${prefixOpt}",\
        installation: "InSearchPath"
    sh '[ ! -d build ] || rm -rf build'
    cmakeBuild generator: 'Unix Makefiles',\
        buildDir: 'build',\
        buildType: 'Release',\
        installation: 'InSearchPath',\
        cmakeArgs: """-DCMAKE_CXX_COMPILER=/opt/rocm/llvm/bin/clang++
                     -DCMAKE_C_COMPILER=/opt/rocm/llvm/bin/clang
                     ${cmakeOpts}
                     """
    sh 'cd build; make -j $(nproc) MIOpenDriver'
}

void showEnv() {
    sh 'hostname'
    sh 'cat /etc/os-release'
    sh 'ulimit -a'
    sh '/opt/rocm/bin/rocm-smi'
}

String dockerArgs() {
    return '--user "$(id -u):$(id -g)" --device=/dev/kfd --device=/dev/dri --group-add video -u 0'
}

String dockerImage() {
    return 'rocm/mlir:rocm4.3-latest'
}

void preMergeCheck() {
    sh '''
    if [ ! -f ./build/compile_commands.json ];  then
      echo "No compile commands, bailing."
      exit 1
    fi
    if [ ! -f ./compile_commands.json ]; then
      ln -s build/compile_commands.json compile_commands.json
    fi
    '''
    sh 'python3 ./mlir/utils/jenkins/static-checks/premerge-checks.py'
}

void testMIOpenDriver(String dtype, String xdlops, String layout, String direction, String tuning) {
    echo "Config is (${dtype}, ${layout}, xdlops=${xdlops}, dir=${direction}, tuning=${tuning})"
    sh 'ldconfig'
    dir('MIOpen/build/') {
        sh """
        bash ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/miopen_validate.sh \
        --layout ${layout} \
        --direction ${direction} \
        --dtype ${dtype} \
        ${xdlops == '1' ? '--xdlops' : '--no-xdlops'} \
        ${tuning == '1' ? '--tuning' : '--no-tuning'} \
        < ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/resnet50-miopen-configs"""
    }
}

void postProcessPerfRes() {
    publishHTML (target: [
        allowMissing: false,
        alwaysLinkToLastBuild: false,
        keepAll: true,
        reportDir: 'build',
        reportFiles: 'MLIR_Performance_Changes.html,MLIR_vs_MIOpen.html',
        reportName: 'Performance report'
    ])

    plot csvFileName: 'plot-nightly-perf-results-000001.csv',\
        csvSeries: [[file: 'build/mlir_vs_miopen_perf_for_plot.csv', displayTableFlag: false]],\
        title: 'Test performance summary',\
        yaxis: 'TFlops',\
        style: 'line',\
        group: 'Performance plots'

    // Save results for future comparison
    archiveArtifacts artifacts: 'build/mlir_*.csv,build/perf-run-date', onlyIfSuccessful: true
}

pipeline {
    agent none
    parameters {
        // Below should be set statically by Jenkins job
        booleanParam(name: 'nightly', defaultValue: params.nightly ? true : false,
                     description: 'Run extra nightly-only tests')
        booleanParam(name: 'canXdlops', defaultValue: params.canXdlops == false ? false : true,
                     description: 'Can this CI instance use xdlops (no for public server)')

        // Each below control whether to run a individual stage from parallel run
        // They default to true but deverloper can toggle them for debugging purpose
        booleanParam(name: 'sharedLib', defaultValue: true,
                     description: 'Run the shared library stage')
        booleanParam(name: 'staticLib', defaultValue: true,
                     description: 'Run the static library stage')
        booleanParam(name: 'perfTest', defaultValue: true,
                     description: 'Run the performance testing stage')
    }
    stages {
        stage("Set System Property") {
            steps {
                script {
                     System.setProperty("org.jenkinsci.plugins.durabletask.BourneShellScript.HEARTBEAT_CHECK_INTERVAL", "86400");
                }
            }
        }
        stage("Build and Test") {
            parallel {
                stage("Shared Library: fixed E2E tests") {
                    when {
                        beforeAgent true;
                        equals expected: true, actual: params.sharedLib;
                    }
                    agent {
                        docker {
                            image dockerImage()
                            args dockerArgs()
                            label "${params.canXdlops ? 'gfx908' : 'rocm' }"
                            alwaysPull true
                        }
                    }
                    stages {
                        stage('Environment') {
                            steps {
                                showEnv()
                            }
                        }
                        stage("Shared library build and fixed tests") {
                            steps {
                                buildProject('check-mlir check-mlir-miopen', """
                                  -DMLIR_MIOPEN_DRIVER_PR_E2E_TEST_ENABLED=${params.nightly ? '0' : '1'}
                                  -DMLIR_MIOPEN_DRIVER_XDLOPS_TEST_ENABLED=${params.canXdlops ? '1' : '0'}
                                  -DMLIR_MIOPEN_DRIVER_E2E_TEST_ENABLED=${params.nightly ? '1' : '0'}
                                  -DMLIR_MIOPEN_DRIVER_MISC_E2E_TEST_ENABLED=${params.nightly ? '1' : '0'}
                                  -DMLIR_MIOPEN_DRIVER_TEST_GPU_VALIDATION=1
                                  -DCMAKE_EXPORT_COMPILE_COMMANDS=1
                                """)
                            }
                        }
                        stage('Static Tests') {
                            when { allOf {
                                equals expected: false, actual: params.nightly;
                                // Avoid this test running twice
                                equals expected: true, actual: params.canXdlops
                            } }
                            steps {
                               preMergeCheck()
                           }
                        }
                     }
                     post {
                        always {
                            cleanWs()
                        }
                    }
                }
                stage("Shared Library: random E2E tests") {
                    when {
                        beforeAgent true;
                        allOf {
                            equals expected: true, actual: params.sharedLib;
                            equals expected: true, actual: params.nightly
                        }
                    }
                    agent {
                        docker {
                            image dockerImage()
                            args dockerArgs()
                            label "${params.canXdlops ? 'gfx908' : 'rocm' }"
                            alwaysPull true
                        }
                    }
                    stages {
                        stage('Environment') {
                            steps {
                                showEnv()
                            }
                        }
                        stage("Shared library build and random tests") {
                            steps {
                                buildProject('check-mlir-miopen',
                                             """-DMLIR_MIOPEN_DRIVER_PR_E2E_TEST_ENABLED=0
                                             -DMLIR_MIOPEN_DRIVER_XDLOPS_TEST_ENABLED=${params.canXdlops ? '1' : '0'}
                                             -DMLIR_MIOPEN_DRIVER_E2E_TEST_ENABLED=1
                                             -DMLIR_MIOPEN_DRIVER_RANDOM_DATA_SEED=1
                                             -DMLIR_MIOPEN_DRIVER_MISC_E2E_TEST_ENABLED=0
                                             -DMLIR_MIOPEN_DRIVER_TEST_GPU_VALIDATION=0
                                             -DCMAKE_EXPORT_COMPILE_COMMANDS=1""")
                            }
                        }
                    }
                    post {
                        always {
                            cleanWs()
                        }
                    }
                }
                stage("Static Library") {
                    when {
                        beforeAgent true;
                        anyOf {
                            equals expected: true, actual: params.staticLib;
                            equals expected: true, actual: params.perfTest;
                        }
                    }
                    agent {
                        docker {
                            image dockerImage()
                            args dockerArgs()
                            label "${params.canXdlops ? 'gfx908' : 'rocm' }"
                            alwaysPull true
                        }
                    }
                    environment {
                        PATH="/opt/rocm/llvm/bin:$PATH"
                    }
                    stages {
                        stage('Environment') {
                            steps {
                                showEnv()
                            }
                        }
                        stage("Igemm build") {
                            steps {
                                checkout scm
                                buildProject('libMLIRMIOpen',
                                             '-DBUILD_FAT_LIBMLIRMIOPEN=ON')
                                cmake arguments: '--install . --component libMLIRMIOpen --prefix /opt/rocm',\
                                installation: 'InSearchPath', workingDir: 'build'
                            }
                        }
                        stage("Build MIOpen with libMLIRMIOpen") {
                            when { allOf {
                                equals expected: true, actual: params.nightly;
                                anyOf {
                                    equals expected: true, actual: params.perfTest;
                                    equals expected: true, actual: params.canXdlops;
                                } }
                            }
                            steps {
                                dir('MIOpen') {
                                getAndBuildMIOpen('--prefix ./MIOpenDeps', '''-DMIOPEN_USE_MLIR=On
                                        -DMIOPEN_BACKEND=HIP
                                        -DCMAKE_PREFIX_PATH="${WORKSPACE}/MIOpen/MIOpenDeps"
                                        -DMIOPEN_USER_DB_PATH=${WORKSPACE}/MIOpen/build/MIOpenUserDB
                                        -DMIOPEN_TEST_FLAGS=\'--verbose --disable-verification-cache\'
                                        ''')
                                }
                            }
                        }
                        stage("Tune MLIR Kernels") {
                            when { allOf{
                                equals expected: true, actual: params.nightly;
                                equals expected: true, actual: params.perfTest;
                            } }
                            steps {
                                buildProject('ci-performance-scripts', '')
                                dir('build') {
                                    sh 'python3 ./bin/MIOpenDriver.py -tuning'
                                }
                            }
                        }
                        stage("Stash MIOpen with libMLIRMIOpen") {
                            when { allOf {
                                equals expected: true, actual: params.nightly;
                                anyOf {
                                equals expected: true, actual: params.perfTest;
                                equals expected: true, actual: params.canXdlops;
                            } } }
                            steps {
                                stash name:"MIOpen-MLIR-kernels",\
                                      includes: """
                                          MIOpen/build/**,\
                                          mlir/utils/jenkins/miopen-tests/**\
                                      """
                            }
                        }
                    }
                    post {
                        always {
                            cleanWs()
                        }
                    }
                }
                stage("Build for Performance Test") {
                    when {
                        beforeAgent true;
                        allOf{
                            equals expected: true, actual: params.perfTest;
                            equals expected: true, actual: params.nightly;
                    } }
                    agent {
                        docker {
                            image dockerImage()
                            args dockerArgs()
                            label "${params.canXdlops ? 'gfx908' : 'rocm' }"
                            alwaysPull true
                        }
                    }
                    environment {
                        PATH="/opt/rocm/llvm/bin:$PATH"
                    }
                    stages {
                        stage("Environment") {
                            steps {
                                showEnv()
                            }
                        }
                        stage("Build MIOpen with HIP") {
                           steps {
                               dir('MIOpen') {
                                   getAndBuildMIOpen('--prefix ./MIOpenDeps',
                                                     '''-DMIOPEN_BACKEND=HIP
                                   -DCMAKE_PREFIX_PATH="${WORKSPACE}/MIOpen/MIOpenDeps"
                                   -DCMAKE_INSTALL_PREFIX=${WORKSPACE}/MIOpen/build/MIOpenInstallDir
                                   ''')
                                   sh 'cd build; make -j $(nproc)'
                                   sh 'cd build; make install'
                               }
                               stash name:"MIOpen-kernels",\
                                   includes: "MIOpen/build/**"
                           }
                        }
                        stage("Build MLIR") {
                            steps {
                                buildProject('mlir-miopen-driver mlir-rocm-runner ci-performance-scripts', '')
                            }
                        }
                        stage("Test MLIR vs MIOpen") {
                            steps {
                                sh 'ldconfig'
                                dir('build') {
                                    sh 'date --utc +%Y-%m-%d > perf-run-date'
                                    // Run MLIR perf benchmarks
                                    sh 'python3 ./bin/MIOpenDriver.py'
                                }
                                stash name:"Perf-report", includes:"build/mlir_vs_miopen_perf.csv"
                            }
                        }
                    }
                    post {
                        always {
                            cleanWs()
                        }
                    }
                }
            }
        }
        stage("Benchmark and Report Performance") {
            when {
                        beforeAgent true;
                        equals expected: true, actual: params.perfTest;
                        equals expected: true, actual: params.nightly;
                    }
                    agent {
                        docker {
                            image dockerImage()
                            args dockerArgs()
                            label "${params.canXdlops ? 'gfx908' : 'rocm' }"
                            alwaysPull true
                        }
                    }
                    environment {
                        // Make libMIOpen.so accessible to the test driver
                        LD_LIBRARY_PATH="${WORKSPACE}/MIOpen/build/lib:$LD_LIBRARY_PATH:"
                   }
            stages {
                stage("Copy MIOpen with MLIR kernels") {
                    steps {
                        sh 'rm -rf MIOpen'
                        unstash name: "MIOpen-MLIR-kernels"
                    }
                }
                stage("Performance Test: Tuned vs Untuned") {
                    steps {
                        buildProject('ci-performance-scripts', '')
                        sh 'ldconfig'
                        dir('build') {
                           sh 'python3 ./bin/MIOpenDriver.py -bmiopen_use_tuned_mlir'
                           sh 'rm -rf ${WORKSPACE}/MIOpen/build/MIOpenUserDB'
                           sh 'python3 ./bin/MIOpenDriver.py -bmiopen_use_untuned_mlir'
                        }
                    }
                }
                stage("Copy earlier performance results") {
                    steps {
                        copyArtifacts filter: 'build/*.csv,build/perf-run-date',\
                                               optional: true,\
                                               flatten: true,\
                                               projectName: "/${JOB_NAME}",\
                                               selector: lastSuccessful(),\
                                               target: 'build/oldData'
                    }
                }

                stage("Create performance reports") {
                    steps {
                        unstash name: "Perf-report"
                        dir('build') {
                            sh 'ls -l'
                            sh 'python3 ./bin/createPerformanceReports.py'
                            sh 'python3 ./bin/perfRegressionReport.py'
                        }
                        postProcessPerfRes()
                    }
                }
            }
            post {
                always {
                    cleanWs()
                }
             }
        }
        stage("MIOpen Resnet50 Config Test") {
            when { allOf {
                equals expected: true, actual: params.nightly;
                equals expected: true, actual: params.canXdlops;
                equals expected: true, actual: params.staticLib;
            }}
            matrix {
                options {
                    skipDefaultCheckout()
                }
                axes {
                    axis {
                        name 'DTYPE'
                        values 'fp16', 'fp32'
                    }
                    axis {
                        name 'XDLOPS'
                        values '1', '0'
                    }
                    axis {
                        name 'LAYOUT'
                        values 'NCHW', 'NHWC'
                    }
                    axis {
                        name 'DIRECTION'
                        values '1', '2', '4'
                    }
                }
                stages {
                    stage('Test MIOpen') {
                        agent {
                            docker {
                                image dockerImage()
                                args dockerArgs()
                                label "${XDLOPS == '1' ? 'gfx908' : 'gfx908'}"
                                alwaysPull true
                            }
                        }
                        stages {
                            stage('Environment') {
                                steps {
                                    echo "Config is ($DTYPE, $LAYOUT, xdlops=$XDLOPS, dir=$DIRECTION)"
                                    showEnv()
                                }
                            }
                            stage("Copy MIOpen test environment") {
                                steps {
                                    unstash name: "MIOpen-MLIR-kernels"
                                }
                            }
                            stage("Test MIOpen config") {
                                environment {
                                    // Make libMIOpen.so accessible to the test driver
                                    LD_LIBRARY_PATH="${WORKSPACE}/MIOpen/build/lib:$LD_LIBRARY_PATH:"
                                }
                                steps {
                                    testMIOpenDriver('$DTYPE', '$XDLOPS', '$LAYOUT', '$DIRECTION', '0')
                                    //testMIOpenDriver('$DTYPE', '$XDLOPS', '$LAYOUT', '$DIRECTION', '1')
                                }
                            }
                        }
                        post {
                            always {
                                cleanWs()
                            }
                        }
                    }
                }
            }
        }
    }
}
