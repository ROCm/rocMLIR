void buildProject(String target, String cmakeOpts) {
    timeout(time: 60, activity: true, unit: 'MINUTES') {
        cmakeBuild generator: 'Ninja',\
            buildDir: 'build',\
            buildType: 'RelWithDebInfo',\
            installation: 'InSearchPath',\
            steps: [[args: target]],\
            cmakeArgs: "-DROCMLIR_DRIVER_ENABLED=1 $cmakeOpts"
    }
}

void buildMIOpen(String cmakeOpts) {
    sh '[ ! -d build ] || rm -rf build'
    timeout(time: 60, activity: true, unit: 'MINUTES') {
        cmakeBuild generator: 'Unix Makefiles',\
            buildDir: 'build',\
            buildType: 'Release',\
            installation: 'InSearchPath',\
            cmakeArgs: """-DCMAKE_CXX_COMPILER=/opt/rocm/llvm/bin/clang++
                         -DCMAKE_C_COMPILER=/opt/rocm/llvm/bin/clang
                         ${cmakeOpts}
                         """
        sh 'cd build; make -j $(nproc) MIOpenDriver'
    }
}

void showEnv() {
    echo "$env.NODE_NAME"
    sh 'hostname'
    sh 'cat /etc/os-release'
    sh 'ulimit -a'
    sh '/opt/rocm/bin/rocm-smi'
}

String dockerArgs() {
    return "--device=/dev/kfd --device=/dev/dri --group-add video --group-add render -v /etc/passwd:/etc/passwd:ro -v /etc/group:/etc/group:ro"
}

String dockerImage() {
    return 'rocm/mlir:rocm5.4-latest'
}


//makes sure multiple builds are not triggered for branch indexing
def resetBuild() {
    if (currentBuild.getPreviousBuild() == null || currentBuild.getPreviousBuild().getBuildCauses().toString().contains('BranchIndexingCause')) {
        def buildNumber = BUILD_NUMBER as int; if (buildNumber > 1) milestone(buildNumber - 1); milestone(buildNumber)
    }
}

def rebootNode() {
    build job: 'maintenance/reboot-slaves', propagate: false , parameters: [string(name: 'server', value: "${env.NODE_NAME}"),]
}

pipeline {
    agent none
    parameters {
        booleanParam(name: 'sharedLib', defaultValue: true,
            description: 'Run shared library tests')
        booleanParam(name: 'staticLib', defaultValue: true,
            description: 'Run static library and MIOpen integration tests')
        string(name: 'MIOpenBranch', defaultValue: 'develop',
            description: 'The MIOpen branch to be used with the job')
        string(name: 'CKBranch', defaultValue: 'develop',
            description: 'The Composable Kernel branch to be used with the job')
    }
    stages {
        stage('Kill old PR builds') {
            steps {
                resetBuild()
            }
        }
        stage('Build and Test') {
            parallel {
                stage("Shared Library: fixed E2E tests") {
                    when {
                        beforeAgent true;
                        equals expected: true, actual: params.sharedLib;
                    }
                    agent {
                        docker {
                            image dockerImage()
                            args dockerArgs()
                            label "rocm"
                            alwaysPull true
                        }
                    }
                    environment {
                        PATH="$PATH:/opt/rocm/llvm/bin"
                    }
                    stages {
                        stage("Environment") {
                            steps {
                                showEnv()
                            }
                        }
                        stage("Shared library build and fixed tests") {
                            steps {
                                buildProject('check-mlir check-rocmlir', """
                                  -DROCMLIR_DRIVER_PR_E2E_TEST_ENABLED=1
                                  -DROCMLIR_GEN_FLAGS="-mfma=off -atomic_add=off"
                                  -DROCMLIR_DRIVER_E2E_TEST_ENABLED=0
                                  -DROCMLIR_DRIVER_TEST_GPU_VALIDATION=1
                                  -DLLVM_LIT_ARGS=-v
                                  -DCMAKE_EXPORT_COMPILE_COMMANDS=1
                                """)
                             }
                        }
                    }
                    post {
                        unsuccessful {
                            rebootNode()
                        }
                        always {
                            cleanWs()
                        }
                    }
                }
                stage("Static Library") {
                    when {
                        beforeAgent true;
                        equals expected: true, actual: params.staticLib;
                    }
                    agent {
                        docker {
                            image dockerImage()
                            args dockerArgs()
                            label "gfx906"
                            alwaysPull true
                        }
                    }
                    environment {
                        PATH="$PATH:/opt/rocm/llvm/bin"
                    }
                    stages {
                        stage("Environment") {
                            steps {
                                showEnv()
                            }
                        }
                        stage("Igemm build") {
                            steps {
                                checkout scm
                                buildProject('librockCompiler',
                                             '-DBUILD_FAT_LIBROCKCOMPILER=ON')
                                cmake arguments: "--install . --component librockCompiler --prefix ${WORKSPACE}/MIOpenDeps",\
                                installation: 'InSearchPath', workingDir: 'build'
                            }
                        }
                        // Note: setting cxxflags here works around https://github.com/ROCmSoftwarePlatform/MIOpen/issues/1604
                        stage("Build MIOpen with librockCompiler") {
                            steps {
                                dir('MIOpen') {
                                git branch: params.MIOpenBranch, poll: false,\
                                    url: 'https://github.com/ROCmSoftwarePlatform/MIOpen.git'
                                buildMIOpen("""-DMIOPEN_USE_MLIR=On
                                        -DMIOPEN_USE_COMPOSABLEKERNEL=Off
                                        -DMIOPEN_BACKEND=HIP
                                        -DCMAKE_PREFIX_PATH=${WORKSPACE}/MIOpenDeps
                                        -DCMAKE_CXX_FLAGS="-isystem ${WORKSPACE}/MIOpenDeps/include"
                                        -DMIOPEN_USER_DB_PATH=${WORKSPACE}/MIOpen/build/MIOpenUserDB
                                        -DMIOPEN_TEST_FLAGS="--verbose --disable-verification-cache"
                                        """)
                                }
                            }
                        }
                        stage("Test selected MIOpen configs") {
                            environment {
                                // Make libMIOpen.so accessible to the test driver
                                LD_LIBRARY_PATH="${WORKSPACE}/MIOpen/build/lib:$LD_LIBRARY_PATH:"
                            }
                            steps {
                                dir('MIOpen/build/') {
                                    sh """
                                    bash ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/miopen_validate.sh --test-all --no-tuning\
                                       < ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/selected-resnet50-miopen-configs"""
                                    sh """
                                    bash ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/miopen_validate.sh --test-all --tuning\
                                       < ${WORKSPACE}/mlir/utils/jenkins/miopen-tests/selected-resnet50-miopen-configs"""
                                }
                            }
                        }
                    }
                    post {
                        unsuccessful {
                            rebootNode()
                        }
                        always {
                            cleanWs()
                        }
                    }
                }
            }
        }
    }
}
